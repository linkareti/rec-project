'*******************************************************
'* experiencia da condução de calor                    *
'* 3 varas metalicas que sao aquecidas simultaneamente *
'* a temperatura das barras e lida em tres pontos      *
'*******************************************************

'{$STAMP BS2}
'{$PBASIC 2.0}

'******* DEFAULT VARIABLES ********
speedCon   CON     16468      '16572/4800 16468/9600 16780/2400 17197/1200
spaceByte  VAR     Byte       'Byte auxiliar (Space)
TX         CON     16
RX         CON     16
i          VAR     Word       'contador
nsamples   VAR     Word       'nº de amostras a adquirir [10..(1000/dt),1]
'******* END DEFAULT VARIABLES *******


'******* SPECIFIC VARIABLES *******
    temp_1  VAR Word          'temperatura no ponto 1 da vara de latao em decimas de grau
    temp_2  VAR Word          'temperatura no ponto 2 da vara de latao em decimas de grau
    temp_3  VAR Word          'temperatura no ponto 3 da vara de latao em decimas de grau
    temp_4  VAR Word          'temperatura no ponto 1 da vara de ferro em decimas de grau
    temp_5  VAR Word          'temperatura no ponto 2 da vara de ferro em decimas de grau
  '  temp_6  VAR Word          'temperatura no ponto 3 da vara de ferro em decimas de grau
   ' temp_7  VAR Word          'temperatura no ponto 1 da vara de cobre em decimas de grau
'    temp_8  VAR Word          'temperatura no ponto 2 da vara de cobre em decimas de grau
 '   temp_9  VAR Word          'temperatura no ponto 3 da vara de cobre em decimas de grau
    dt      VAR Byte          'tempo entre amostras em segundos [1..20,2]
    dt_aq   VAR Byte          'tempo de aquecimento em segundos [10..100,2] e multiplo inteiro de dt
    temp_max VAR Byte         'Temperatura maxima a atingir e manter [25..75,1]
    rele     CON 8            'define o pino 8 como interruptor do rele
    latencia CON 200
    modo VAR Bit             'modo=0 regime normal; modo=1 regime oscilante
'******* END SPECIFIC VARIABLES *******


'********************************************
'Configuração inicial
InitConfig:  'exemplo de uma rotina de inicializacao
    LOW rele
    OUTPUT rele
    DIRD=%0000
    DIRC=%0000

'********************************************

 'Reset - retorna à base...
Reset:
  GOSUB FlashLight 'Pisca a luzinha em cada reset
'********************************************

'Não foi configurado
Not_Configured:
  SEROUT TX,speedCon,["CONFIG_START_NOT_DONE",CR]
'********************************************

'********************************************
'Aguarda um ConfigStart pacientemente, enquanto vai informando quem é!
MainWait:
  SEROUT TX,speedCon,["ELAB_CONDCALOR_STAMP_V1.0",CR]        ' Identifica-se
  SERIN RX,speedCon,5000,Not_Configured,[WAIT("cfg "),DEC3 dt,spaceByte,DEC3 dt_aq,spaceByte,DEC3 temp_max,spaceByte,DEC4 nsamples,spacebyte,BIN modo]

'********************************************
'Ok, configurado! Get Ready...
Configured:
  SEROUT TX,speedCon,[CR,"CONFIG_START_ACCEPTED",CR]  'Aceitou a configuracao
  IF temp_max < 700 THEN AcqLoopEnter
  temp_max = 700

'********************************************
'Set... Go!
AcqLoopEnter:
  SEROUT TX,speedCon,["STARTED",CR]    ' Avisa que vai arrancar a rotina de aquisicao

AcqLoop:
    HIGH rele
    FOR i=1 TO nsamples
  latao:
    PULSIN 15,1,temp_1
    PULSIN 14,1,temp_2
    PULSIN 13,1,temp_3
    SEROUT TX,speedCon,["Temp: ",DEC i," ",DEC temp_1/28," ",DEC temp_2/28," ",DEC temp_3/28," "]  ' Display data.

  ferro:
    PULSIN 12,1,temp_4
    PULSIN 11,1,temp_2
    PULSIN 10,1,temp_3
    SEROUT TX,speedCon,[DEC temp_4/28," ",DEC temp_2/28," ",DEC temp_3/28," "] ' Display data.

  cobre:
    PULSIN 9,1,temp_5
    PULSIN 7,1,temp_2
    PULSIN 5,1,temp_3
    SEROUT TX,speedCon,[DEC temp_5/28," ",DEC temp_2/28," ",DEC temp_3/28,$D,$A]     ' Display data.

    PAUSE dt*1000-latencia

  IF modo=1 THEN Oscila

    IF ( ((i*dt)<dt_aq) AND (temp_max > temp_1/28) AND (temp_max > temp_4/28) AND (temp_max > temp_5/28))THEN SKIPit 'limita temperatura para estudo estacionario de duas condicoes fronteira

    LOW rele
    GOTO SKIPit:

  Oscila:
    IF i*dt<>dt_aq/2 THEN Skipit:
    TOGGLE RELE

  SKIPit:

    NEXT

    LOW rele

    GOTO MainWait
'********************************************

'********************************************
' Função faz piscar 4 vezes o led para debug
FlashLight:
  LOW 6
  FOR i=0 TO 4
    OUT6 = 0
    PAUSE 200
    OUT6 = 1
    PAUSE 200
  NEXT
  OUT6 = 0
RETURN
'********************************************