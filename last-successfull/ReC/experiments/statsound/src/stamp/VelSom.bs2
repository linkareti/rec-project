'{$STAMP BS2}

'******* DEFAULT VARIABLES ********

speedCon     CON     16468      '16572/4800 16468/9600 16780/2400 17197/1200
spaceByte    VAR     Byte       'Byte auxiliar (Space)
TX           CON     16
RX           CON     16
i            VAR     Word       'contador auxiliar
j            VAR     Word       'contador auxiliar
nsamples     VAR     Word       'nº de amostras a adquirir

'******* END DEFAULT VARIABLES *******


'******* SPECIFIC VARIABLES *******

Pos     DATA(2)            'ultima posicao do step motor
PW      VAR   Word         'Largura do pulso em unidades de 2us (media movel)
Width   VAR   Word         'Largura do pulso em unidades de 2us
Temp    VAR   Word         'Temperatura
x       VAR   Word         'posicao a atingir
xini    VAR   Word         'posição inicial  0 < xini < 130
xfin    VAR   Word         'posição final    0 < xfin < 130
dx      VAR   Word         'step em x
p       VAR   Word         'posicao absoluta
status  VAR   Nib          'estado dos relés
switch  VAR   Bit          'estado do switch de calibração
reset   VAR   Bit          'bit para calibrar ou não
TcLow   CON   10245        'Numero de contagens para temperatura minima
TcHigh  CON   11079        'Numero de contagens para temperatura maxima
TLow    CON   14           'Temperatura minima de calibracao em milesimas de grau centigrado
THigh   CON   23           'Temperatura maxima de calibracao em milesimas de grau centigrado
xconv   CON   17           'conversão stepvoltas para decimas de mm 1stepvolta = 1,7mm

'********************************************************************
' a resolução maxima sao 1,7mm pois uma stepvolta <=> 1,7mm         *
' nao da para fazer andar menos do que isso                         *
' é estranho pois a printer devia ter uma resolucao bem maior!!!    *
' é preciso alterar a funçao posi para usar unidades menores que    *
' stepvoltas porque 1 volta da 1,7mm                                *
'********************************************************************

'******* END SPECIFIC VARIABLES *******


'********************************************
'Configuração inicial

LOW 7                    '
switch = 0               '
status = 0               'Desliga todos os LEDs
DIRD=%1111               'Nible D cofigurado como Output
DIRB=%1111               'Nible B cofigurado como Output
DIRC=%1111               'Nible C cofigurado como Output
OUTD=%0000               'driver do stepmotor a zero
READ pos,p               'guarda a posição na var pos
x = p                    'actualiza a variavel x
INPUT 3                  'para ler o switch da posiçao zero

'********************************************
'Reset - retorna à base...
  GOSUB Blink              'pisca pisca
  GOSUB calib              'faz sempre uma calibração inicial
'  GOTO LOOP
  GOTO Test
'********************************************
'Não foi configurado
Not_Configured:
  SEROUT TX,speedCon,["CONFIG_START_NOT_DONE",CR]

'********************************************
'Aguarda um ConfigStart pacientemente, enquanto vai informando quem é!

LOOP:
  LOW   OUTB
  SEROUT TX,speedCon,["READY",$D,$A]
  SERIN RX,speedCon,5000,Not_Configured,[WAIT("cfg "),DEC3 nsamples,spaceByte,DEC3 xini,spaceByte,DEC3 xfin,spaceByte,BIN4 status,spaceByte,BIN1 reset]

'********************************************
'Ok, configurado! Get Ready...
Configured:
  dx = (xfin-xini)/nsamples      'calcula o step entre pontos

Teste_status:
  IF (status.BIT1=0) THEN ENDIF:
  status.BIT2=0                  'nao podemos ter o BIT1 e o BIT2 ligados ao mesmo tempo!

ENDIF:
  OUTC = status                  'altera as saidas dos relés
  IF (reset = 1) THEN calibrar   'faz a calibração caso necessario
  GOTO cicle1
calibrar
  GOSUB calib                    'chama a rotina de calibração

  x = xini                       'coloca o embolo na posicao inicial
  GOSUB posi

'********************************************
'Set... Go!
cicle1:
  GOSUB temperature              'mede a temperatura do ar antes da experiencia
  SEROUT TX,speedCon,["Temp: ",DEC3 Temp,spaceByte,"Status: ",BIN4 status,$D,$A]
  FOR j=0 TO nsamples

    IF (xini > xfin) THEN Forward    'sentido positivo ou negativo
    GOTO Backward
Forward:
    x = xini - (j*dx)            'incrementa o step em x
    GOTO Continue
Backward:
    x = xini + (j*dx)
Continue:
    GOSUB posi                   'vai para a posição calculada
    SEROUT TX,speedCon,["POS: ",DEC3 x,$D,$A]
    PAUSE 100
    NEXT
  GOSUB temperature              'mede a temperatura do ar depois da experiencia
  SEROUT TX,speedCon,["Temp: ",DEC3 Temp,spaceByte,"Status: ",BIN4 status,$D,$A]
  OUTD=%0000                     'coloca a low o driver do stepmotor

'********************************************
'Aquisition end
  GOTO LOOP                      'volta ao LOOP inicial

'*************************************
'Função que le a temperatura
Temperature:
  PULSIN 0,1,Width
  PW=((PW/5)*4)+(Width/5)
  Temp=(THigh-TLow)*(PW-TcLow)/(TcHigh-TcLow)+Tlow
  RETURN

'*************************************
'Função que faz mover o embolo
Posi:
  IF x>p THEN go_up
  IF x<p THEN go_down
  RETURN

go_up:
FOR i=1 TO x-p
OUTD=%0001
PAUSE 5
OUTD=%0011
PAUSE 5
OUTD=%0010
PAUSE 5
OUTD=%0110
PAUSE 5
OUTD=%0100
PAUSE 5
OUTD=%1100
PAUSE 5
OUTD=%1000
PAUSE 5
OUTD=%1001
PAUSE 5
NEXT
p = x
WRITE pos,p
RETURN

go_down:
FOR i=1 TO p-x
OUTD=%0001
PAUSE 5
OUTD=%1001
PAUSE 5
OUTD=%1000
PAUSE 5
OUTD=%1100
PAUSE 5
OUTD=%0100
PAUSE 5
OUTD=%0110
PAUSE 5
OUTD=%0010
PAUSE 5
OUTD=%0011
PAUSE 5
NEXT
p = x
WRITE pos,p
RETURN


'***************************************
'Função que faz a calibracao
calib:
  switch = IN3
  IF switch = 0 THEN goto_calib
  p = 130
  WRITE pos,p
  reset = 0
  RETURN

goto_calib:

  OUTD=%0001
  PAUSE 1
  OUTD=%0011
  PAUSE 1
  OUTD=%0010
  PAUSE 1
  OUTD=%0110
  PAUSE 1
  OUTD=%0100
  PAUSE 1
  OUTD=%1100
  PAUSE 1
  OUTD=%1000
  PAUSE 1
  OUTD=%1001
  PAUSE 1
  GOTO calib


'*****************************************
'Blink do led para debug
Blink:

FOR i=0 TO 4
  OUT7=1
  PAUSE 200
  OUT7=0
  PAUSE 200
  NEXT
RETURN

'*****************************************
'para estudar as estacionarias
Test:
  FOR j = 0 TO 130
  	x = j
    GOSUB posi
    PAUSE 1000
    SEROUT TX,speedCon,["POS: ",DEC3 x,$D,$A]
    NEXT
  FOR j = 0 TO 130
  	x = 130 - j
    GOSUB posi
    PAUSE 1000
    SEROUT TX,speedCon,["POS: ",DEC3 x,$D,$A]
    NEXT
  GOTO Test




