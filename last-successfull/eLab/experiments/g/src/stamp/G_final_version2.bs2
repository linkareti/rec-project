'{$STAMP BS2}

'Nota que existe um protocolo nas mensagens:
' PC -> STAMP - minúsculas
' STAMP -> PC - maiúsculas

'******* DEFAULT VARIABLES ********

speedCon     CON     16390      '16390/34000 16572/4800 16468/9600 16780/2400 17197/1200
spaceByte  VAR  Byte      'Byte auxiliar (Space)
TX    CON  16
RX    CON  16
i    VAR   Word
nsamples  VAR  Word      'nº de amostras a adquirir
'******* END DEFAULT VARIABLES *******


'******* SPECIFIC VARIABLES *******
disparo    CON  9  ' pin utilizado para fazer o disparo da bola
trigger    CON  10  ' pin utilizado para fazer o trigger do sensor
sensorin  CON  11  ' pin de input que le o PWM do sensor
delay_time  VAR  Word  ' delay entre aquisições (0 => 73Hz (13.7ms); 100 => 8,878Hz(113ms) TBS[s]=0.0028+altura [m]*0.01+delay_time/1000)
'        min=10 max=250
altura    VAR  Word  ' altura da bola ALTURA=0.000346*rctime [m]
power    VAR  Word  ' potencia do disparo (0->100; 0 =>salto minimo;  100 => salto max)
'******* END SPECIFIC VARIABLES *******


'********************************************
'Configuração inicial

InitConfig:      'exemplo de uma rotina de inicializacao
  LOW disparo
  LOW trigger
  DIRC = %0011    'disparo e trigger como output e sensorin como input
  GOSUB FlashLight

'********************************************
'Não foi configurado
Not_Configured:
  SEROUT TX,speedCon,["CONFIG_START_NOT_DONE",CR]


'********************************************
'Aguarda um ConfigStart pacientemente, enquanto vai informando quem é!
MainWait:
  SEROUT TX,speedCon,["ELAB_G_STAMP_V02",CR]  ' Identifica-se
  SERIN RX,speedCon,5000,Not_Configured,[WAIT("cfg "),DEC4 nsamples,spaceByte,DEC3 power,spaceByte,DEC3 delay_time]

'********************************************
'Ok, configurado! Get Ready...
Configured:

  SEROUT TX,speedCon,[CR,"CONFIG_START_ACCEPTED",CR]  'Aceitou a configuracao
  PAUSE 2000    'espera que a bola pare

'********************************************
'Set... Go!

AcqLoopEnter:
  SEROUT TX,speedCon,["STARTED",CR]    ' Avisa que vai arrancar a rotina de aquisicao

AcqLoop:
  PULSOUT disparo,(power*5 + 4000)

  FOR i=0 TO nsamples
    LOW trigger        ' diz ao sensor para começar a medir
    RCTIME  sensorin,0,altura    ' mede o tempo que o sensor esta a zero
    HIGH trigger
    SEROUT TX,speedCon,["HEIGHT ",DEC4 altura,CR]  ' devolve o valor medido
    PAUSE delay_time      ' espera x tempo entre aquisiçoes
  NEXT

AcqLoopEnd:
  GOTO MainWait

'********************************************
' Função faz piscar 4 vezes o led verde para debug
FlashLight:
  LOW 7
  FOR i=0 TO 5
    OUT7 = 0
    PAUSE 200
    OUT7 = 1
    PAUSE 200
  NEXT
  OUT7 = 0
RETURN
'********************************************